# helper module used by the multi-match-query. It takes a list of fieldnames (or a hash with
# containing the query-time boost factors as values) and returns a function that
# transforms an AST as generated by the query-parser into an Elasticsearch Query.
ConfigNode = require "../config-node"
Merge = require "../merger"
merge = Merge()
{isArray} = require "util"
empty = (thing)->
  if not thing?
    return true
  if isArray(thing) and thing.length is 0
    return true
  if typeof thing is "object" and Object.keys(thing).length is 0
    return true
  
joinConsecutiveTerms = (operands)->
  reducer = (done, next)->
    [nextOp, nextArgs...] = next
    [prefix..., prev] = done
    [prevOp, prevArgs...] = (prev ? [])

    
    if prevOp is 'TERM' and nextOp is 'TERM'
      [prefix..., [prevOp, prevArgs..., nextArgs...]]
    else
      [done..., next]

  operands.reduce reducer, []

builtInTransform = (ast, cx)->
  [opc, operands...] = ast
  {fieldBoosts, defaultOperator, transformChild} = cx
  switch opc
    when 'TERM'
      multi_match:
        query: operands.join ' '
        type: 'cross_fields'
        fields: fieldBoosts
        operator: defaultOperator
    when "DQUOT"
      multi_match:
        query: operands.join ' '
        type: 'phrase'
        fields: fieldBoosts
    when "SQUOT"
      multi_match:
        query: operands.join ' '
        type: 'phrase'
        fields: fieldBoosts
    when "OR"
      bool:
        should: operands.map transformChild cx
    when "AND"
      bool:
        must: operands.map transformChild cx
    when "NOT"
      bool:
        must_not: operands.map transformChild cx
    when "SEQ"
      operands = joinConsecutiveTerms operands
      if operands.length is 1
        (transformChild cx) operands[0]
      else
        switch defaultOperator?.toLowerCase().trim()
          when 'and'
            bool:
              must: operands.map transformChild cx
          else
            bool:
              should: operands.map transformChild cx
    else
      match_all:{}

module.exports = class AstTransformer extends ConfigNode

  queryFields: (attrs)->
    queryFields = {}
    if @_options.fields?
      return @_options.fields

    for attr in attrs when attr.query
      fieldName = if attr.includeSubfields then attr.field+"*" else attr.field
      queryFields[fieldName] = attr.boost ? 1
    queryFields

  transform: (ast, attributes, query)->
    queryFields = @queryFields attributes
    if empty(queryFields)
      return {}
    fieldBoosts = if isArray(queryFields) then queryFields else ("#{fieldName}^#{boost}" for fieldName, boost of queryFields)
    fieldNames = if isArray(queryFields) then queryFields else Object.keys queryFields

    defaultOperator = query?.qop ? @_options?.defaultOperator ? "and"


    customize = @_options.customize
    customizedTransform = (ast, cx)->
      [operation, operands...] = ast
      part0 =builtInTransform ast, cx

      customization = customize?[operation] ? {}
      if typeof customization is "function"
        customization operands, customizedTransform, part0
      else if typeof customization is "object"
        merge part0, customization

    transformChild =(cx)->(child)->customizedTransform child, cx
    cx0 ={queryFields,attributes,fieldNames, fieldBoosts, defaultOperator, transformChild}

    body = (if ast?.length > 0 then customizedTransform(ast, cx0) else {})
    @postProcess body, cx0

  postProcess: (body, options)->
    if @_options?.postProcess?
      @_options.postProcess body, options
    else
      body
